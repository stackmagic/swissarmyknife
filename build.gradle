
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

description = 'Miscellaneous java utilities'
group       = 'net.swisstech'
version     = '1.2.3'

sourceCompatibility = 1.7
targetCompatibility = 1.7

// normal repositories and dependencies
repositories {
	mavenLocal()
	mavenCentral()
	jcenter()
}

dependencies {
	compile 'ch.bind:philib:0.12.0'
	compile 'net.swisstech:logger:1.1.0'

	testCompile 'org.testng:testng:6.8.8'
	testCompile 'org.mockito:mockito-all:1.10.8'
	testCompile 'ch.qos.logback:logback-classic:1.1.2'
}

// testing
test {
	useTestNG()
}

// TODO use eclipse-enhancer once it is published
eclipseProject.doLast {
	sourceSets*.java.srcDirs*.each      { it.mkdirs() }
	sourceSets*.resources.srcDirs*.each { it.mkdirs() }
}
// END TODO

// task to generate wrapper
task wrapper(type: Wrapper) {
	gradleVersion = '2.2.1'
}

// source-code jar
task sourceJar(type: Jar) {
	from sourceSets.main.allSource
}

// bintray uses the publication, also includes source jar
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact sourceJar {
				classifier 'sources'
			}
		}
	}
}

// uploading to jcenter
buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.6'
	}
}

def getProperty = { String name ->
	try {
		return project[name]
	}
	catch (MissingPropertyException e) {
		println "!!! property ${name} is not available"
		return ""
	}
}

bintray {
	// key and user must be in your ~/.gradle/gradle.properties
	key          = getProperty('swisstech_bintray_apikey')
	user         = getProperty('swisstech_bintray_user')
	dryRun       = false
	publish      = true
	publications = [ 'mavenJava' ]
	pkg {
		repo            = 'maven'
		name            = project.name
		desc            = project.description
		licenses        = [ 'Apache-2.0' ]
		websiteUrl      = "https://github.com/stackmagic/${project.name}"
		vcsUrl          = "https://github.com/stackmagic/${project.name}.git"
		issueTrackerUrl = "https://github.com/stackmagic/${project.name}/issues"
		publicDownloadNumbers = true
		version {
			name = project.version
			desc = project.description
		}
	}
}
